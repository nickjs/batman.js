<!DOCTYPE html>
<html>
<head>
    <title>BatMap</title>
    <meta http-equiv="X-UA-Compatible" content="IE=Edge" />
    <script src="http://ajax.googleapis.com/ajax/libs/jquery/1.6.2/jquery.min.js" type="text/javascript"></script>
    <script type="text/javascript" src="http://maps.googleapis.com/maps/api/js?sensor=false"></script>
    <script type="text/javascript" src="../lib/es5-shim.js"></script>
    <script type="text/javascript" src="../lib/batman.js"></script>
    <script type="text/javascript" src="../lib/batman.solo.js"></script>
    <script type="text/javascript" src="../lib/coffee-script.js"></script>
</head>

<body>
    <div id="container">
        <h1>BatMap</h1>
        <form data-formfor-char="controllers.app.emptyChar" data-event-submit="controllers.app.create">
          <input class="new-item" placeholder="Name" data-bind="char.name" />
          <input class="new-item" placeholder="Address" data-bind="char.address" />
          <input type="checkbox" class="new-item" data-bind="char.isBatman" /> is batman
          <button type="submit">Add</button>
        </form>
          <ul id="items">
            <li data-foreach-char="Character.all">
                <span data-bind="char.name"></span> <span data-bind="char.isBatman"></span>
                <a data-event-click="char.destroy">Delete</a>
                <div data-view="GoogleMapsView" data-view-address="char.address" ></div>
            </li>
            <li><span data-bind="'characters' | pluralize Character.all.length"></span></li>
        </ul>
       
    </div>

    <script type="text/coffeescript">
                
    # Create our application and namespace.
    window.BatMap = class BatMap extends Batman.App

      # setup our root route. When the app starts up, it will automatically call AppController::index
      @root 'app#index'

      @on 'ready', ->
        console.log "BatMap ready"

    class BatMap.GoogleMapsView extends Batman.View
      @option 'address'

      @accessor 'imageSrc'
        get: -> "http://maps.googleapis.com/maps/api/staticmap?center=#{@get('address')}&zoom=14&size=400x400&sensor=false"
          
      render: ->
        img = $('<img>').attr('src', @get('imageSrc'))
        $(@get('node')).append(img)

    Character = class BatMap.Character extends Batman.Model
      @resourceName: 'character'
      @persist Batman.LocalStorage
      @encode 'name', 'isBatman', 'address'

    AppController = class BatMap.AppController extends Batman.Controller
      routingKey: 'app'
      
      index: ->
        @set 'emptyChar', new Character
        # add some example characters to show off.
        Character.load (error, characters) ->
          # you always want to make sure you handle errors (more elegantly than this) when writing connection code
          throw error if error
          unless characters and characters.length
            callback = (error) -> throw error if error
            new Character(name: 'Bruce Wayne', isBatman: true, address: "New York, NY").save(callback)
            new Character(name: 'Dick Grayson', address: "Alcatraz").save(callback)
            new Character(name: 'Alfred Pennyworth', address: "Sydney, NSW").save(callback)
        @render false

      create: ->
        @get('emptyChar').save (error, record) =>
          throw error if error
          @set 'emptyChar', new Character

    # Start the app. This will start up the dispatcher and a number of other mechanisms.
    BatMap.run()
    </script>
</body>
</html>